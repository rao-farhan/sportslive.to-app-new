import java.util.Properties

plugins {
    alias(libs.plugins.androidApplication) apply false
    alias(libs.plugins.androidMultiplatformLibrary) apply false
    alias(libs.plugins.composeMultiplatform) apply false
    alias(libs.plugins.composeCompiler) apply false
    alias(libs.plugins.kotlinMultiplatform) apply false
    alias(libs.plugins.kotlinJvm) apply false
    alias(libs.plugins.kotlinxSerialization) apply false
    alias(libs.plugins.sqldelight) apply false
}

// Reusable function to get required properties from local.properties
fun Project.getRequiredProperty(key: String): String {
    val localPropertiesFile = rootProject.file("local.properties")
    if (!localPropertiesFile.exists()) {
        throw GradleException("local.properties file is required for build. Please create it based on local.properties.example")
    }
    val properties = Properties().apply {
        localPropertiesFile.inputStream().use { load(it) }
    }
    return properties.getProperty(key) ?: throw GradleException("Property '$key' is required in local.properties")
}

// Store the function in extra properties so subprojects can access it
extra.set("getRequiredProperty", { key: String -> getRequiredProperty(key) })

tasks.register("syncIosConfig") {
    val iosConfigFile = rootProject.file("iosApp/Configuration/Config.xcconfig")

    doLast {
        val appId = getRequiredProperty("APP_ID")
        val appVersionName = getRequiredProperty("APP_VERSION_NAME")
        val appVersionCode = getRequiredProperty("APP_VERSION_CODE")
        val teamId = getRequiredProperty("TEAM_ID")

        val configContent = """
            // This file is auto-generated by gradle syncIosConfig task.
            // Do not edit manually.
            
            TEAM_ID=$teamId
            PRODUCT_NAME=SportsLive
            PRODUCT_BUNDLE_IDENTIFIER=$appId
            MARKETING_VERSION=$appVersionName
            CURRENT_PROJECT_VERSION=$appVersionCode
        """.trimIndent()

        iosConfigFile.parentFile.mkdirs()
        iosConfigFile.writeText(configContent)
        println("Synced local.properties to iosApp/Configuration/Config.xcconfig")
    }
}

project(":shared").tasks.matching { it.name.contains("embedAndSign") }.configureEach {
    dependsOn("syncIosConfig")
}
